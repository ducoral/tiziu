            expression ::= logical eof  

               logical ::= relational logical_rest

          logical_rest ::= empty
                         | '&&' logical
                         | '||' logical

            relational ::= arithmetic relational_rest

       relational_rest ::= empty
                         | relational_operators relational
                  
  relational_operators ::= '=='
                         | '!='
                         | '>'
                         | '>='
                         | '<'
                         | '<='

            arithmetic ::= multiply_divide arithmetic_rest

       arithmetic_rest ::= empty
                         | '+' multiply_divide 
                         | '-' multiply_divide

       multiply_divide ::= term multiply_divide_rest

  multiply_divide_rest ::= empty
                         | '*' term
                         | '/' term

                  term ::= literal
                         | dotted_identifer
                         | '[' logical '?' logical ':' logical ']' 
                         | '(' logical ')'
                         | function

     dotted_identifier ::= identifier dotted_identifier_rest

dotted_identifier_rest ::= empty
                         | '.' identifier dotted_identifier_rest

              function ::= identifier '(' function_parameters ')'
   
   function_parameters ::= empty
                         | parameters
   
            parameters ::= identifier ':' logical parameters_rest
                    
       parameters_rest ::= empty
                         | ',' parameters
       
            identifier ::= /[a-zA-Z_][a-zA-Z0-9_]*/
            
               literal ::= null
                         | string
                         | integer
                         | decimal
                         | boolean